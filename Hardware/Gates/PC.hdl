// This file is part of www.nand2tetris.org
// and the book "The Elements of Computing Systems"
// by Nisan and Schocken, MIT Press.
// File name: projects/03/a/PC.hdl

/**
 * A 16-bit counter with load and reset control bits.
 * if      (reset[t] == 1) out[t+1] = 0
 * else if (load[t] == 1)  out[t+1] = in[t]
 * else if (inc[t] == 1)   out[t+1] = out[t] + 1  (integer addition)
 * else                    out[t+1] = out[t]
 */


CHIP PC {
    IN in[16], load, inc, reset;
    OUT out[16];

    PARTS:
    Register(in=writeBus, load=true, out=regOut, out=out); 
    Inc16(in=regOut, out=incBus); // wire register output to incrementer
    
    Mux16(a=regOut, b=incBus, sel=inc, out=incOut); // route either the incremented out or the register out
    Mux16(a=incOut, b=in, sel=load, out=loadOut); // wire the input or incremented out
    Mux16(a=loadOut, b=false, sel=reset, out=writeBus); // load the next value as 0 or the assigned load value
}
